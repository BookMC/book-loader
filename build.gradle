//file:noinspection GroovyAssignabilityCheck
plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom" version "1.2.0"
}

ext {
    def env = System.getenv()
    local = !(env.GITHUB_ACTIONS || env.JITPACK)
}

group "org.bookmc"
version "0.5.0${local ? "+local" : ""}"

repositories {
    mavenCentral()
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    include
    implementation.extendsFrom(include)
}


dependencies {
    // Libraries to be downloaded via the installer
    api group: "org.apache.logging.log4j", name: "log4j-api", version: "2.0-beta9"
    api group: "org.apache.logging.log4j", name: "log4j-core", version: "2.0-beta9"
    api "com.github.BookMC:LegacyLauncher:2cbe27165a"

    // Shaded libraries
    include("org.spongepowered:mixin:0.7.11-SNAPSHOT") {
        exclude module: "guava"
        exclude module: "commons-io"
        exclude module: "gson"
        exclude module: "launchwrapper"
        exclude module: "log4j-core"
    }

    compileOnly "com.google.code.gson:gson:2.2.4" // BookMC/minecraft#28

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.0"

    testRuntimeOnly "com.google.code.gson:gson:2.2.4" // BookMC/minecraft#28
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

blossom {
    replaceToken("%LOADER_VERSION%", project.version)
}

jar {
    from {
        configurations.include.collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude "module-info.class", "LICENSE.*", "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"
}

publishing {
    repositories {
        maven {
            url = "$buildDir/repository"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "loader"
                url = "https://github.com/BookMC/loader"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}