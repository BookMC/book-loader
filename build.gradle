//file:noinspection GroovyAssignabilityCheck
plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom" version "1.2.0"
    id "org.bookmc.buildtools" version "0.3.3+build.15"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

ext {
    def env = System.getenv()
    buildNumber = env.BUILD_NUMBER
    local = buildNumber == null
    branch = "git rev-parse --abbrev-ref HEAD".execute().text
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
    }
}

group "org.bookmc"
// We add the suffix for (our) non-lts support.
// Java 8 support will be removed in the future however.
version "0.8.22+${buildNumber ? "build.$buildNumber" : "local"}"

repositories {
    mavenCentral()
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        url "https://maven.bookmc.org"
    }
    maven {
        url "https://maven.fabricmc.net"
    }
    maven {
        url "https://jitpack.io"
    }
}


configurations {
    include
    implementation.extendsFrom(include)
}


dependencies {
    // Libraries to be downloaded via the installer
    compileOnly "org.apache.logging.log4j:log4j-api:2.0-beta9"
    compileOnly "org.apache.logging.log4j:log4j-core:2.0-beta9"

    // Directly in Minecraft
    compileOnly "com.google.code.gson:gson:2.2.4" // BookMC/minecraft#28

    // Shaded libraries
    include("net.fabricmc:sponge-mixin:0.9.4+mixin.0.8.2") {
        exclude module: "log4j-api"
        exclude module: "log4j-core"
        exclude module: "guava"
        exclude module: "gson"
    }
    include("org.bookmc:install:0.1.0") {
        exclude module: "log4j-api"
        exclude module: "log4j-core"
    }

    // Minecraft (1.8.9) bundles an earlier version of Guava
    // so to main compatibility we must shade and relocate it
    include "com.google.guava:guava:30.1.1-jre"

    include "org.ow2.asm:asm:9.2"
    include "org.ow2.asm:asm-tree:9.2"
    include "org.ow2.asm:asm-commons:9.2"
    include "org.ow2.asm:asm-util:9.2"

    include "com.github.zafarkhaja:java-semver:0.9.0"
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

blossom {
    replaceToken("%LOADER_VERSION%", project.version)
}

shadowJar {
    configurations = [project.configurations.include]
    archiveClassifier.set("")
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude "module-info.class", "LICENSE.*", "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"

    // Relocate guava
    // Minecraft (1.8.9) bundles an earlier version of Guava & Log4j
    // so to main compatibility we must shade and relocate it
    relocate "com.google.common", "org.bookmc.loader.libs.guava.common"
    relocate "com.google.errorprone", "org.bookmc.loader.libs.guava.errorprone"
    relocate "com.google.j2objc", "org.bookmc.loader.libs.guava.j2objc"
    relocate "com.google.thirdparty", "org.bookmc.loader.libs.guava.thirdparty"
}

tasks.build.dependsOn(tasks.shadowJar)

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "book-loader"
                url = "https://github.com/BookMC/loader"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}