//file:noinspection GroovyAssignabilityCheck
plugins {
    id "java"
    id "java-library"
    id "maven-publish"
    id "net.kyori.blossom" version "1.2.0"
    id "com.github.johnrengelman.shadow" version "7.0.0"
}

ext {
    def env = System.getenv()
    local = !env.GITHUB_ACTIONS
    jdk = JavaVersion.current().getMajorVersion()
    branch = "git rev-parse --abbrev-ref HEAD".execute().text
}

group "org.bookmc"
// We add the suffix for (our) non-lts support.
// Java 8 support will be removed in the future however.
version "0.8.13${local ? "-local" : ""}${JavaVersion.current() != JavaVersion.VERSION_1_8 ? "+jdk.$jdk" : ""}"

repositories {
    mavenCentral()
    maven {
        url "https://repo.spongepowered.org/repository/maven-public/"
    }
    maven {
        url "https://maven.bookmc.org"
    }
    maven {
        url "https://jitpack.io"
    }
}


configurations {
    include
    implementation.extendsFrom(include)
}


dependencies {
    // Libraries to be downloaded via the installer
    api group: "org.apache.logging.log4j", name: "log4j-api", version: "2.0-beta9"
    api group: "org.apache.logging.log4j", name: "log4j-core", version: "2.0-beta9"

    // Directly in Minecraft
    compileOnly "com.google.code.gson:gson:2.2.4" // BookMC/minecraft#28

    // Shaded libraries
    include "org.spongepowered:mixin:0.8.4-SNAPSHOT"
    include "org.bookmc:install:0.1.0"
    include "com.fasterxml.jackson.core:jackson-core:2.13.0-rc1"

    // Minecraft (1.8.9) bundles an earlier version of Guava
    // so to main compatibility we must shade and relocate it
    include "com.google.guava:guava:30.1.1-jre"

    include "org.ow2.asm:asm:9.2"
    include "org.ow2.asm:asm-tree:9.2"
    include "org.ow2.asm:asm-commons:9.2"
    include "org.ow2.asm:asm-util:9.2"

    include "com.github.zafarkhaja:java-semver:0.9.0"
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

blossom {
    replaceToken("%LOADER_VERSION%", project.version)
}

shadowJar {
    configurations = [project.configurations.include]
    archiveClassifier.set("")
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    exclude "module-info.class", "LICENSE.*", "META-INF/*.RSA", "META-INF/*.SF", "META-INF/*.DSA"

    // Relocate guava
    // Minecraft (1.8.9) bundles an earlier version of Guava
    // so to main compatibility we must shade and relocate it
    relocate "com.google.common", "org.bookmc.loader.libs.guava.common"
    relocate "com.google.errorprone", "org.bookmc.loader.libs.guava.errorprone"
    relocate "com.google.j2objc", "org.bookmc.loader.libs.guava.j2objc"
    relocate "com.google.thirdparty", "org.bookmc.loader.libs.guava.thirdparty"
}

tasks.build.dependsOn(tasks.shadowJar)

publishing {
    repositories {
        maven {
            def env = System.getenv()

            boolean isEnvVarsAvailable = env.containsKey("MAVEN_USER") && env.containsKey("MAVEN_PASSWORD")

            if (isEnvVarsAvailable) {
                credentials {
                    username env.get("MAVEN_USER")
                    password env.get("MAVEN_PASSWORD")
                }
            }

            url = local || !isEnvVarsAvailable ? "$buildDir/repository" : "https://maven.bookmc.org/releases/"
        }
    }

    publications {
        //noinspection GroovyAssignabilityCheck
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = "book-loader"
                url = "https://github.com/BookMC/loader"

                developers {
                    developer {
                        name = "ChachyDev"
                    }
                }
            }
        }
    }
}